(use 'defs')

(let gen-ir-arg \arg ->
  (match arg::'tag'
    'int' => {
      'type': ARG-INT
      'value': arg::'items'::0::'lexeme'
    }
    'var' => {
      'type': ARG-VAR
      'value': arg::'items'::0::'lexeme'
    }))

(let gen-ir-instr \instr last ->
  (match instr::'tag'
    'return' => [
      {
        'type': INSTR-RETURN
        'args': [
          (gen-ir-arg instr::'items'::0)
        ]
        'last': last
      }
    ]
    'assign' => [
      {
        'type': INSTR-ASSIGN
        'dest': instr::'items'::0::'items'::0::'lexeme'
        'args': [
          (gen-ir-arg instr::'items'::1)
        ]
        'last': last
      }
    ]
    'call' => [
      {
        'type': INSTR-CALL
        'func': instr::'items'::0::'items'::0::'lexeme'
        'args': []
        'last': last
      }
    ]
    ... =>
      (do
        (error 'Unknown AST tag: `' instr::'tag' '`')
        (exit 1))))

(let gen-ir-global-instr \instr last ->
  (match instr::'tag'
    'proc-def' => [
      {
        'type': 'proc'
        'name': instr::'items'::0::'lexeme'
        'body': (gen-ir-items instr::'items'::1::'items' gen-ir-instr)
      }
    ]))

(let gen-ir-items \items gen-func ->
  (match (len items)
    0   => []
    1   => (gen-func items::0 true)
    ... => (+ (gen-func items::0 false) (gen-ir-items (from items 1) gen-func))))

(let gen-ir \program ->
  (gen-ir-items program::'items'::0::'items' gen-ir-global-instr))
