(use 'defs')
(use 'lexer')

(macro !? \expr ->
  (let value expr)
  (if (error? value)
    (ret value))
  value)

(let parser-init \tokens ->
  {
    'tokens': tokens
    'pos': 0
    'max-pos': 0
  })

(let new-error \parser message ->
  {
    'parser': parser
    'error': {
      'message': message
    }
  })

(let new-unexpected-error \parser ->
  (let pos parser::'pos')
  (if (< pos (len parser::'tokens'))
    (let lexeme parser::'tokens'::pos::'lexeme')
    (new-error parser
               (join ['Unexpected `' lexeme '`'] ''))
   else
    (new-error parser 'Unexpected EOF')))

(let error? \value ->
  (not-unit? value::'error'))

(let parse-token \type name ->
  \parser ->
    (let pos parser::'pos')
    (if (< pos (len parser::'tokens'))
      (match parser::'tokens'::pos::'type'
        type =>
          (do
            (set parser::'pos' (+ parser::'pos' 1))
            (if (< parser::'max-pos' parser::'pos')
              (set parser::'max-pos' parser::'pos'))
            (let item (with parser::'tokens'::pos 'name' name))
            {
              'parser': parser
              'item': item
            })
        ...  => (new-unexpected-error parser))
     else
      (new-unexpected-error parser)))

(let name-token \type name -> (parse-token type name))
(let skip-token \type -> (parse-token type unit))

(let parse-node-inner \parses ->
  \parser ->
    (if (== (len parses) 0)
      (ret {
        'parser': parser
        'item': {
          'items': []
        }
      }))

    (let first (!? (parses::0 parser)))
    (let rec (!? ((parse-node-inner (from parses 1)) first::'parser')))
    (if (and (unit? first::'item'::'tag') (unit? first::'item'::'name'))
      {
        'parser': rec::'parser'
        'item': {
          'items': rec::'item'::'items'
        }
      }
     else
      {
        'parser': rec::'parser'
        'item': {
          'items': (before first::'item' rec::'item'::'items')
        }
      }))

(let parse-node \tag parses ->
  \parser ->
    (if (== (len parses) 0)
      (ret (new-error parser 'Empty node' true)))

    (let first (parses::0 parser))
    (if (error? first)
      (ret (new-error first::'parser' first::'error'::'message')))

    (let rec (!? ((parse-node-inner (from parses 1)) first::'parser')))
    (if (and (unit? first::'item'::'tag') (unit? first::'item'::'name'))
      {
        'parser': rec::'parser'
        'item': {
          'tag': tag
          'items': rec::'item'::'items'
        }
      }
     else
      {
        'parser': rec::'parser'
        'item': {
          'tag': tag
          'items': (before first::'item' rec::'item'::'items')
        }
      }))

(let parse-list \tag parse ->
  \parser ->
    (let first (parse parser))
    (if (error? first)
      (set parser::'max-pos' first::'parser'::'max-pos')
      (ret {
        'parser': parser
        'item': {
          'tag': tag
          'items': []
        }
      }))

    (let rec (!? (<-> first::'parser')))

    {
      'parser': rec::'parser'
      'item': {
        'tag': tag
        'items': (before first::'item' rec::'item'::'items')
      }
    })

(let parse-select \parses ->
  \parser ->
    (if (== (len parses) 0)
      (ret (new-unexpected-error parser)))

    (let first (parses::0 parser))
    (set parser::'max-pos' first::'parser'::'max-pos')
    (if (error? first)
      (ret ((parse-select (from parses 1)) parser)))

    first)

(let parse-expr \->
  (parse-select [
    (parse-node 'int' [
      (name-token TT-INT 'value')
    ])
    (parse-node 'var' [
      (name-token TT-IDENT 'name')
    ])
  ]))

(let parse-statement \->
  (parse-select [
    (parse-node 'return' [
      (skip-token TT-RETVAL)
      (parse-expr)
    ])
    (parse-node 'assign' [
      (parse-node 'var' [
        (name-token TT-IDENT 'name')
      ])
      (skip-token TT-ASSIGN)
      (parse-expr)
    ])
    (parse-node 'call' [
      (parse-node 'func' [
        (name-token TT-IDENT 'name')
      ])
      (skip-token TT-OPAREN)
      (skip-token TT-CPAREN)
    ])
  ]))

(let parse-proc-def \->
  (parse-node 'proc-def' [
    (skip-token TT-PROC)
    (name-token TT-IDENT 'name')
    (skip-token TT-OPAREN)
    (skip-token TT-CPAREN)
    (parse-list 'body'
      (parse-statement))
    (skip-token TT-END)
  ]))

(let parse-program \->
  (parse-node 'program' [
    (parse-list 'items'
      (parse-select [
        (parse-proc-def)
      ]))
  ]))

(let parse \tokens ->
  (let parser (parser-init tokens))
  (let result ((parse-program) parser))
  (if (error? result)
    (error result::'error'::'message')
    (exit 1))

  (if (< result::'parser'::'max-pos' (len result::'parser'::'tokens'))
    (let max-pos result::'parser'::'max-pos')
    (let lexeme result::'parser'::'tokens'::max-pos::'lexeme')
    (error 'Unexpected `' lexeme '`')
    (exit 1))

  result::'item')
