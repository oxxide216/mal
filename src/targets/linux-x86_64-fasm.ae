(use 'alloc')

(let LINUX-x86_64-FASM-ASM-BEGIN
  (join [
    'format ELF64 executable\n'
    'segment readable executable\n'
    'entry _start\n'
    '_start:\n'
    '  call main\n'
    '  mov rdi,rax\n'
    '  mov rax,60\n'
    '  syscall\n'
  ] ''))

(let LINUX-x86_64-FASM-GENERAL-REGS [
  'rbx' ; callee-saved
  'r10' ; not callee-saved
  'r11' ; not callee-saved
  'r12' ; callee-saved
  'r13' ; callee-saved
  'r14' ; callee-saved
  'r15' ; callee-saved
])

(let arg-gen-asm-linux-x86_64-fasm \arg meta ->
  (match arg::'type'
    ARG-INT => arg::'value'
    ARG-VAR => meta::(do arg::'value')::'loc'))

(let return-gen-asm-linux-x86_64-fasm \instr meta ->
  (let end (if instr::'last' '' else '  ret\n'))
  (match instr::'args'::0
    unit => end
    ...  =>
      (do
        (let loc (arg-gen-asm-linux-x86_64-fasm instr::'args'::0 meta))
        (if (== loc 'rax')
          end
         else
          (join ['  mov rax,' loc '\n' end] '')))))

(let assign-gen-asm-linux-x86_64-fasm \instr meta ->
  (join [
    '  mov '
    meta::(do instr::'dest')::'loc'
    ','
    (arg-gen-asm-linux-x86_64-fasm instr::'args'::0 meta)
    '\n'
  ] ''))

(let call-gen-asm-linux-x86_64-fasm \instr meta ->
  (join [
    '  call '
    instr::'func'
    '\n'
  ] ''))

(let instr-gen-asm-linux-x86_64-fasm \instr meta ->
  (match instr::'type'
    INSTR-RETURN => (return-gen-asm-linux-x86_64-fasm instr meta)
    INSTR-ASSIGN => (assign-gen-asm-linux-x86_64-fasm instr meta)
    INSTR-CALL   => (call-gen-asm-linux-x86_64-fasm instr meta)))

(let proc-gen-asm-linux-x86_64-fasm \proc ->
  (let asm
    (map
      \instr -> (instr-gen-asm-linux-x86_64-fasm instr proc::'meta') <>
      proc::'body'))
  (join
    (+ [proc::'name' ':\n']
       (after asm '  ret\n'))
    ''))

(let proc-alloc-regs-linux-x86_64-fasm \proc ->
  (with proc 'meta' (alloc-regs proc::'meta' LINUX-x86_64-FASM-GENERAL-REGS)))

(let gen-asm-linux-x86_64-fasm \ir ->
  (let full-ir (map proc-alloc-regs-linux-x86_64-fasm ir))
  (let asm (map proc-gen-asm-linux-x86_64-fasm full-ir))
  (+ LINUX-x86_64-FASM-ASM-BEGIN (join asm '')))
